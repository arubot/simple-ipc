plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.2'
    id 'org.jetbrains.dokka' version '0.9.17'
}

group = 'pw.aru.utils'
version = '2.1'

// Minified Splash
({ String l, String v ->
    def d = "$l ${(v.size() + l.size() + 1) % 2 != 0 ? "-" : "--"} v$v"
    def w = (0..(30 - d.size() / 2)).collect { it % 2 != 0 ? '-' : '=' }.join ''
    println "|$w| $d |${w.reverse()}|"
})("Aru Simple IPC Library", version)

repositories {
    jcenter()
    maven { url = 'https://dl.bintray.com/adriantodt/maven' }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'pw.aru.utils:pipes:1.1.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.1.7'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task docsZip(type: Zip, dependsOn: dokka) {
    classifier = 'docs'
    from "$buildDir/dokka"
}

jar {
    baseName = project.name
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

artifacts {
    archives javadocJar, sourcesJar, docsZip
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/arubot/simple-ipc.git'
        publish = true
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn 'publishBintrayReleasePublicationToMavenLocal'
}
